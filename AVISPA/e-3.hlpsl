role alice ( 
	A,B:agent,
	Ya,Yb:public_key,
	Ia:nat,
	P,M,Q:nat,
	H2,H3:text,
	Sign,KDF,CERT,MUL,PI,LOW,HIG,MAC: function,
	SND,RCV:channel(dy)
	)
played_by A def=
local 
	Ib,K_a,K_b:nat, 
	Ka,Kb,CertificateB,AutB,Qba,Tba,AutA,TagA,Kba,Mba,CertificateA,H4:message,
	T1,H1:text,
	State : nat
init State := 0
transition

1. State = 0 /\ RCV(start)
=|> State' := 1 /\
K_a' := new() /\
Ka':=MUL(K_a'.P) /\
SND(Ia.Ka') /\ witness(A,B,ss0,Ka')

2. State = 1 /\ RCV(Ib'.CertificateB'.Kb'.AutB'.{MAC(H2.PI(MUL(K_b'.P)).PI(Ka).Ib'.Ia)}_HIG(KDF(PI(MUL(M.Q.K_b'.Ka)).Ia.Ib'))) 
/\ CertificateB' = CERT(Ib.Yb) /\ AutB' = {Sign(PI(Kb').PI(Ka).Ia)}_inv(Yb)
=|>
State' := 2 /\ 
Qba' := MUL(M.Q.K_a.Kb') /\
Tba' := KDF(PI(Qba').Ia.Ib') /\
Kba' := LOW(Tba') /\
Mba' := HIG(Tba') /\
AutA' := {Sign(PI(Ka).PI(Kb').Ib')}_inv(Ya) /\
TagA' := {H3.PI(Ka).PI(Kb').Ia.Ib'}_Mba' /\
CertificateA' := CERT(Ia.Ya) /\
SND(CertificateA'.AutA'.TagA') /\ secret(Kba,secka,A) /\
request(A,B,ss1,Ib'.CertificateB'.Kb'.AutB'.{MAC(H2.PI(MUL(K_b'.P)).PI(Ka).Ib'.Ia)}_HIG(KDF(PI(MUL(M.Q.K_b'.Ka)).Ia.Ib'))) /\
witness(A,B,ss2,CertificateA'.AutA'.TagA')




end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role bob (
	A,B:agent,
	Ya,Yb:public_key,
	Ib:nat,
	P,M,Q:nat,
	H2,H3:text,
	Sign,KDF,CERT,MUL,PI,LOW,HIG,MAC:function,
	SND,RCV:channel(dy)
	)	
played_by B def=
local
	Ia:nat,
	K_b,K_a:nat,
	Ka,Kb,Qab,Tab,Kab,Mab,AutB,TagB,AutA,TagA,CertificateB,CertificateA:message,
	T2,T1:text,
	State:nat
init State := 10
transition

10. State =10 /\ RCV(Ia'.Ka')
=|> State' := 20 /\ 
K_b' := new() /\
Kb' := MUL(K_b'.P) /\
Qab' := MUL(M.Q.K_b'.Ka') /\
Tab' := KDF(PI(Qab').Ia'.Ib) /\
Kab' := LOW(Tab') /\
Mab' := HIG(Tab') /\
AutB' := {Sign(PI(Kb').PI(Ka').Ia')}_inv(Yb) /\
TagB' := {MAC(H2.PI(Kb').PI(Ka').Ib.Ia')}_Mab' /\
CertificateB' := CERT(Ib.Yb) /\
SND(Ib.CertificateB'.Kb'.AutB'.TagB') /\ secret(Kab',seckb,B) %{MAC(H2.PI(MUL(K_b'.P)).PI(Ka').Ib.Ia')}_HIG(KDF(PI(MUL(M.Q.K_b'.Ka')).Ia'.Ib))
/\ witness(B,A,ss1,Ib.CertificateB'.Kb'.AutB'.{MAC(H2.PI(MUL(K_b'.P)).PI(Ka').Ib.Ia')}_HIG(KDF(PI(MUL(M.Q.K_b'.Ka')).Ia'.Ib)))/\ request(B,A,ss0,Ka')

20. State = 20 /\ RCV(CertificateA'.AutA'.{H3.PI(Ka).PI(Kb).Ia.Ib}_HIG(KDF(PI(MUL(M.Q.K_a'.Kb)).Ia.Ib))) /\ CertificateA' = CERT(Ia.Ya) /\ AutA' = {Sign(PI(Ka).PI(Kb).Ib)}_inv(Ya) 
=|>
State' := 40 /\ 
request(B,A,ss2,CertificateA'.AutA'.{H3.PI(Ka).PI(Kb).Ia.Ib}_HIG(KDF(PI(MUL(M.Q.K_a'.Kb)).Ia.Ib)))

% End

end role
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role session ( A,B:agent,Ya,Yb:public_key,Ia,Ib:nat,P,M,Q:nat,H2,H3:text,Sign,KDF,CERT,MUL,PI,LOW,HIG,MAC:function )
def=
local SA,SB,RA,RB : channel (dy)

composition

alice(A,B,Ya,Yb,Ia,P,M,Q,H2,H3,Sign,KDF,CERT,MUL,PI,LOW,HIG,MAC,SA,RA) 
/\ bob(A,B,Ya,Yb,Ib,P,M,Q,H2,H3,Sign,KDF,CERT,MUL,PI,LOW,HIG,MAC,SB,RB)

end role

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

role environment()
def=

const a,b:agent,
ss1,ss0,ss2,secka,seckb: protocol_id,
ya,yb:public_key,
ia,ib:nat,
p,m,q:nat,
h2,h3:text,
sign:function,
kdf:function,
cert:function,
mul:function,
pi:function,
low:function,
hig:function,
mac:function

intruder_knowledge = {a,b,ya,yb,sign,kdf,cert,mul,pi,low,hig,mac,p,m,q,h2,h3}

composition

session(a,b,ya,yb,ia,ib,p,m,q,h2,h3,sign,kdf,cert,mul,pi,low,hig,mac)

end role

goal
	authentication_on ss1
	%%authentication_on ss0
	authentication_on ss2
	secrecy_of seckb
	secrecy_of secka
end goal

environment()











